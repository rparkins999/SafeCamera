buildscript {
    repositories {
        mavenCentral()
    }
}

plugins {
    id 'com.android.application'
}

def keystorePropertiesFile = rootProject.file("../Keys/keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

def gitdata = new ByteArrayOutputStream()
// change this line to /* if not using git
exec {
    commandLine "git", "--no-pager", "show", "--date=format:%H:%M:%S GMT%z %a %b %d %Y", "-s", "--format=from commit %H of %ad"
    standardOutput = gitdata;
}
//*/

android {
    namespace 'uk.co.yahoo.p1rpp.safecamera'
    compileSdk 32

    defaultConfig {
        applicationId "uk.co.yahoo.p1rpp.safecamera"
        minSdk 24
        // BluetoothAdapter.disable and .enable are not allowed from API 33
        // onwards, so don't increase the targetSdk value.
        //noinspection OldTargetApi
        targetSdk 32
        versionCode 1
        versionName "1.0"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            delete("build/intermediates/incremental/mergeReleaseResources/merger.xml")
            resValue("string", 'build_time', "${(new Date()).format('HH:mm:ss \'GMT\'Z EEE dd MMM yyyy')}")
            resValue("string", 'build_git', "${gitdata.toString()}")
        }
        debug {
            minifyEnabled false
            delete("build/intermediates/incremental/mergeDebugResources/merger.xml")
            resValue("string", 'build_time', "${(new Date()).format('HH:mm:ss \'GMT\'Z EEE dd MMM yyyy')}")
            resValue("string", 'build_git', "${gitdata.toString()}")
        }
    }

    signingConfigs {
        config {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}
